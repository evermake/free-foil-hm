token UVarIdent ('?' letter (letter | digit | '_')*) ; ;

PatternVar. Pattern ::= Ident ;

EVar.    Exp3 ::= Ident ;
ETrue.   Exp3 ::= "true" ;
EFalse.  Exp3 ::= "false" ;
ENat.    Exp3 ::= Integer ;
EAdd.    Exp2 ::= Exp2 "+" Exp3 ;
ESub.    Exp2 ::= Exp2 "-" Exp3 ;
EIf.     Exp1 ::= "if" Exp1 "then" Exp1 "else" Exp1 ;
EIsZero. Exp2 ::= "iszero" "(" Exp ")" ;
ETyped.  Exp  ::= Exp1 ":" Type ;
ELet.    Exp1 ::= "let" Pattern "=" Exp1 "in" ScopedExp ;
EAbs.    Exp1 ::= "λ" Pattern ":" Type "." ScopedExp ;
EApp.    Exp1 ::= Exp1 Exp2 ;
ETAbs.   Exp1 ::= "Λ" TypePattern "." ScopedExp ;
ETApp.   Exp1 ::= Exp1 "[" Type "]" ;
EFor.    Exp1 ::= "for" Pattern "in" "[" Exp1 ".." Exp1 "]" "do" ScopedExp ;

ScopedExp. ScopedExp ::= Exp1 ;

-- https://bnfc.readthedocs.io/en/latest/lbnf.html#coercions
coercions Exp 3 ;

TUVar.  Type ::= UVarIdent ;
TNat.   Type ::= "Nat" ;
TBool.  Type ::= "Bool" ;
TArrow. Type ::= Type "->" Type ;
TVar.   Type ::= Ident ;
TForAll. Type ::= "forall" TypePattern "." ScopedType ;

ScopedType. ScopedType ::= Type ;

TPatternVar. TypePattern ::= Ident ;
